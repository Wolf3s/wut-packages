_realname=mbedtls
pkgname=wiiu-${_realname}
pkgver=2.28.8
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
makedepends=('wiiu-cmake' 'wiiu-pkg-config' 'dkp-toolchain-vars')
groups=('wiiu-portlibs')

source=( 
  "${_realname}-${pkgver}.tar.gz::https://github.com/ARMmbed/mbedtls/archive/refs/tags/v${pkgver}.tar.gz"
  "mbedtls-${pkgver}.patch"
)

build() {
  cd ${_realname}-${pkgver}

  patch -Np1 -i $srcdir/mbedtls-${pkgver}.patch

  ./scripts/config.pl set MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY

  ./scripts/config.pl unset MBEDTLS_SELF_TEST

  /opt/devkitpro/portlibs/wiiu/bin/powerpc-eabi-cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/wiiu \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TESTING=Off -DENABLE_PROGRAMS=Off \
    .

  make
}

package() {
  cd ${_realname}-${pkgver}

  source ${DEVKITPRO}/wiiuvars.sh

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
}

sha256sums=('4fef7de0d8d542510d726d643350acb3cdb9dc76ad45611b59c9aa08372b4213'
            'f70c5f7b74c525ba4a55d477c444bd27f3a040fe73c5d3aa2893ca5e9d0541fb')
